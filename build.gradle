plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
    id 'maven-publish'
    id 'java-library'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

group = project.maven_group
archivesBaseName = project.archives_base_name
version = project.mod_version

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    api "org.apache.xmlgraphics:batik-all:${project.batik_version}"
    include "org.apache.xmlgraphics:batik-all:${project.batik_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileJava {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

sourcesJar {
    from sourceSets.main.allSource
}

remapJar { }

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'fabatik'

            pom {
                name = 'Fabatik'
                description = "A dynamic texture resolution mod and API."
                url = 'https://github.com/LazuriteMC/Fabatik'
                developers {
                    developer {
                        id = 'innatebit'
                        name = 'InnateBit'
                        email = 'patrickhofmanndesigns@gmail.com'
                    }
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            url = 'https://lazurite.dev/maven/releases'
            credentials {
                username = project.property("publish.lazurite.alias")
                password = project.property("publish.lazurite.secret")
            }
        }
    }
}