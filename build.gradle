plugins {
    id 'fabric-loom' version "$loom_version"
    id 'maven-publish'
}

group 'dev.lazurite'
version fabatik_version

dependencies {
    mappings loom.officialMojangMappings()
    minecraft "com.mojang:minecraft:$minecraft_version"
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    api "org.apache.xmlgraphics:batik-all:${project.batik_version}"
    include "org.apache.xmlgraphics:batik-all:${project.batik_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of min_java_version
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

tasks.withType(ProcessResources) {
    inputs.properties(
            'version': project.version,

            'min_java_version': min_java_version,
            'min_minecraft_version': min_minecraft_version,
            'min_fabric_loader_version': min_fabric_loader_version
    )

    filesMatching 'fabric.mod.json', {
        expand(
                'version': project.version,

                'min_java_version': min_java_version,
                'min_minecraft_version': min_minecraft_version,
                'min_fabric_loader_version': min_fabric_loader_version
        )
    }
}

publishing {
    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'

            credentials {
                username property('publish.lazurite.alias')
                password property('publish.lazurite.secret')
            }
        }
    }

    publications {
        fabatik MavenPublication, {
            from components.java
        }
    }
}
